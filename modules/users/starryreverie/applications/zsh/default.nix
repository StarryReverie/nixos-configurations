{
  config,
  lib,
  pkgs,
  ...
}:
let
  selfCfg = config.custom.users.starryreverie or { };
  customCfg = selfCfg.applications.zsh or { };
in
{
  config = {
    custom.users.starryreverie = {
      applications.zsh = lib.mkIf (customCfg.enable or false) {
        alwaysSourceProfile = true;

        rcContent = lib.mkMerge [
          ''
            # ===== Prompt style
            source ${./short-cwd.sh}
            source ${./get-cwd-git-branch.sh}
            setopt prompt_subst
            export PS1='%{%F{226}%}%n%{%F{220}%}@%{%F{214}%}%m%{%F{red}%}$(get-cwd-git-branch) %{%F{45}%}$(short-cwd) %{%F{white}%}($(date +%H:%M))
            %{%f%}> '
            export RPROMPT="%F{red}%(?..%?)%f"
          ''

          ''
            # ===== Zsh autosuggestions
            source ${pkgs.zsh-autosuggestions}/share/zsh-autosuggestions/zsh-autosuggestions.zsh
            ZSH_AUTOSUGGEST_STRATEGY=(history)
          ''

          (lib.mkOrder 1100 ''
            # ===== Zsh-vi-mode
            source ${./zvm-config.sh}
            source ${pkgs.zsh-vi-mode}/share/zsh-vi-mode/zsh-vi-mode.plugin.zsh
          '')

          (lib.mkOrder 1200 ''
            # ===== Zsh syntax highlighting
            # It should be loaded after all other widgets have been loaded.
            source ${pkgs.zsh-syntax-highlighting}/share/zsh-syntax-highlighting/zsh-syntax-highlighting.zsh
            ZSH_HIGHLIGHT_HIGHLIGHTERS+=()
          '')
        ];

        shellAliases = {
          c = "cd ..";

          sy = "sudo systemctl";
          sys = "sudo systemctl start";
          syt = "sudo systemctl stop";
          syr = "sudo systemctl restart";
          syst = "systemctl status";
          syl = "journalctl -xeu";

          syu = "systemctl --user";
          syus = "systemctl --user start";
          syut = "systemctl --user stop";
          syur = "systemctl --user restart";
          syust = "systemctl --user status";
          syul = "journalctl --user -xeu";
        };

        historyFile = lib.mkIf config.preservation.enable "/nix/persistence/home/starryreverie/.zsh_history";
      };
    };
  };
}
